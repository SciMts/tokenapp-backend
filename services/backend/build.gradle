plugins {
    id 'java'
    id 'org.springframework.boot' version '1.5.4.RELEASE'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

apply plugin: 'java'

jar {
    manifest {
        attributes 'Main-Class': 'io.modum.tokenapp.backend.BackendApplication'
    }
}

repositories {
    jcenter()
    mavenCentral()
    maven { url 'https://dl.bintray.com/ethereum/maven/' }
}

springBoot {
    classifier = 'boot'
    executable = true
}

dependencies {

    // Spring Boot
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-devtools'
    compile 'org.springframework.boot:spring-boot-starter-data-jpa'
    compile 'org.springframework.boot:spring-boot-starter-mail'
    compile 'org.springframework.boot:spring-boot-starter-actuator'
    compile 'org.springframework.boot:spring-boot-starter-thymeleaf'
    compile 'org.springframework.retry:spring-retry'
    compile 'io.springfox:springfox-swagger-ui:2.6.1'
    compile 'io.springfox:springfox-swagger2:2.6.1'

    compile 'commons-io:commons-io:2.5'

    // file-based queue
    compile 'com.squareup:tape:1.2.3'
    // qr-code library
    compile 'com.github.kenglxn.QRGen:javase:2.3.0'

    runtime 'net.logstash.logback:logstash-logback-encoder:4.11'

    // Database drivers
    runtime 'org.postgresql:postgresql'
    runtime 'com.h2database:h2'

    // Ethereum and Bitcoin libs
    compile 'org.ethereum:ethereumj-core:1.5.+'
    compile 'org.ethereum:solcJ-all:0.4.8'
    compile 'org.bitcoinj:bitcoinj-core:0.14.4'
    compile 'org.web3j:core:2.3.0'
    compile 'org.knowm.xchange:xchange-kraken:4.2.0'
    compileOnly 'org.springframework.boot:spring-boot-configuration-processor'
    testCompile 'org.springframework.boot:spring-boot-starter-test'
}

// gradle run is alias for bootRun
task run(dependsOn: bootRun)

task bootRunProd(type: org.springframework.boot.gradle.run.BootRunTask, dependsOn: build) {
    doFirst() {
        main = project.mainClassName
        classpath = sourceSets.main.runtimeClasspath
        systemProperty 'spring.profiles.active', 'prod'
    }
}

task stage(type: Copy, dependsOn: [clean, build]) {
    from jar.archivePath
    into project.rootDir
    rename {
        'app.jar'
    }
}
stage.mustRunAfter(clean)

clean << {
    project.file('app.jar').delete()
}
